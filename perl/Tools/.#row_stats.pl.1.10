#!/usr/bin/perl

use strict;
use warnings;

require "$ENV{MYPERLDIR}/lib/format_number.pl";
require "$ENV{MYPERLDIR}/lib/libstats.pl";
require "$ENV{MYPERLDIR}/lib/libfile.pl";

$| = 1;

my @flags   = (
                  [     '-q', 'scalar',     0,     1]
                , [     '-h', 'scalar',     1, undef]
                , [     '-k', 'scalar',     0, undef]
                , [    '-sc', 'scalar',     1, undef]
                , [    '-ec', 'scalar',    -1, undef]
                , [   '-abs', 'scalar',     0,     1]
                , [  '-mean', 'scalar',     0,     1]
                , ['-median', 'scalar',     0,     1]
                , [   '-std', 'scalar',     0,     1]
                , [ '-count', 'scalar',     0,     1]
                , [   '-max', 'scalar',     0,     1]
                , [   '-min', 'scalar',     0,     1]
                , ['-argmax', 'scalar',     0,     1]
                , ['-argmin', 'scalar',     0,     1]
                , [   '-sum', 'scalar',     0,     1]
                , [     '-d', 'scalar',  "\t", undef]
                , [ '--file', 'scalar',   '-', undef]
              );

my %args = %{&parseArgs(\@ARGV, \@flags)};

if(exists($args{'--help'}))
{
   print STDOUT <DATA>;
   exit(0);
}

my $verbose        = not($args{'-q'}); # default is verbose
my $header_rows    = $args{'-h'};
my $key_column     = $args{'-k'};
my $start_column   = $args{'-sc'};
my $end_column     = $args{'-ec'};
my $take_abs       = $args{'-abs'};
my $compute_mean   = $args{'-mean'};
my $compute_median = $args{'-median'};
my $compute_std    = $args{'-std'};
my $compute_count  = $args{'-count'};
my $compute_max    = $args{'-max'};
my $compute_min    = $args{'-min'};
my $compute_argmax = $args{'-argmax'};
my $compute_argmin = $args{'-argmin'};
my $compute_sum    = $args{'-sum'};
my $delim          = $args{'-d'};
my $file           = $args{'--file'};

print "Key";
if ($compute_mean)   { print "\tMean";   }
if ($compute_median) { print "\tMedian"; }
if ($compute_std)    { print "\tStd";    }
if ($compute_count)  { print "\tCount";  }
if ($compute_max)    { print "\tMax";    }
if ($compute_min)    { print "\tMin";    }
if ($compute_argmax) { print "\tArgMax"; }
if ($compute_argmin) { print "\tArgMin"; }
if ($compute_sum)    { print "\tSum";    }
print "\n";

open(FILE, "<$file") or die "ERROR: Can't open file $file for reading!";

my $numNonNumericInHeader = 0;
my $numItemsInHeader = 0;
for (my $i = 0; $i < $header_rows; $i++) { 
    my $line = <FILE>;
    chomp($line);
    my @row = split(/\t/, $line);
    shift(@row); # remove the front item
    foreach my $item (@row) {
	if ($item !~ /^[0-9|.|,|e]*$/) {
	    $numNonNumericInHeader++;
	}
	$numItemsInHeader++;
    }
}

if ($verbose && ($numNonNumericInHeader == 0) && ($numItemsInHeader > 0) ) {
    # everything in the header was a number, so this means the user
    # PROBABLY forgot to properly specify the number of items in the
    # header
    print STDERR
	  "WARNING: row_stats.pl: every item in the header was a number.\n"
	. "         This indicates that you may have forgotten to specify -h 0 (no header lines).\n"
	. "         If input file \"$file\" does NOT have a header line, you MUST specify -h 0, or\n"
	. "         the first line in the file will be assumed to be a header and will be ignored.\n"
	. "         If you are obtaining this message in error, you can specify -q to suppress this output.\n";
}


while(<FILE>)
{
  chomp;

  my @row = split(/\t/);

  if(defined($row[$key_column]))
  {
     my @vec;

     my $last_column = $end_column == -1 ? (@row - 1) : $end_column;

     for (my $i = $start_column; $i <= $last_column; $i++)
     {
       my $x = $row[$i];
       if($x eq 'NaN' or ($x !~ /\S/))
       {
          $x = undef;
       }
       elsif($take_abs)
       {
          $x = $x > 0 ? $x : -$x;
       }
       $vec[$i - $start_column] = $x;
     }

     print "$row[$key_column]";

     if ($compute_mean)
     {
       my $stat = &format_number(&vec_avg(\@vec), 3);
       print "\t$stat";
     }
     if ($compute_median)
     {
       my $stat = &format_number(&vec_median(\@vec), 3);
       print "\t$stat";
     }

     if ($compute_std)
     {
       my $std = &format_number(&vec_std(\@vec), 3);
       my $result = (defined($std) and length($std) > 0) ? sprintf('%f',$std) : 'NaN';
       print "\t", $result;
     }

     if ($compute_count)
     {
       my $count = &vec_count_full_entries(\@vec);
       print "\t$count";
     }

     if ($compute_max)
     {
       my $max = &vec_max(\@vec);
       my $result = (defined($max) and length($max) > 0) ? sprintf('%f',$max) : 'NaN';
       print "\t", $result;
     }

     if ($compute_min)
     {
       my $min = &vec_min(\@vec);
       my $result = (defined($min) and length($min) > 0) ? sprintf('%f',$min) : 'NaN';
       print "\t", $result;
     }

     if ($compute_argmax)
     {
       my ($arg, $max) = &vec_max(\@vec);
       my $result = (defined($arg) and length($arg) > 0) ? sprintf('%i',$arg+1) : 'NaN';
       print "\t", $result;
     }

     if ($compute_argmin)
     {
       my ($arg, $min) = &vec_min(\@vec);
       my $result = (defined($arg) and length($arg) > 0) ? sprintf('%i',$arg+1) : 'NaN';
       print "\t", $result;
     }

     if ($compute_sum)
     {
       my $sum = &vec_sum(\@vec);
       my $result = (defined($sum) and length($sum) > 0) ? sprintf('%f',$sum) : 'NaN';
       print "\t", $result;
     }

     print "\n";
  }
}
close(FILE);

__DATA__

row_stats.pl <data file>

   Computes stats for rows. Different commands can be selected.

   NOTE: It is assumed that there is one header line by default!

   -h <num>:  Number of header rows (default: 1)
   -k <num>:  The key column (default: 0)

   -sc <num>: Start column of the data in each row (default: 1)
   -ec <num>: End column of the data in each row (default: -1, means last columns)

Note that the specified results are always printed out the
same fixed order--it does NOT matter which order you
specify them on the command line.

   -mean:     Compute mean for each row
   -median:   Compute the median for each row
   -abs:      Take the absolute value of each data point (?)
   -std:      Compute standard deviation for each row
   -count:    Count the number of non-empty entries in each row
   -max:      Compute the maximum for each row
   -min       Compute the minimum for each row
   -sum       Compute the sum of each row
   -argmax    Print which column has the maximum value
   -argmin    Print which column has the minimum value

